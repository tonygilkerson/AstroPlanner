APSF,V1,0,,-1,0,
// For each object, find nearest bright star (mag < 3.0)
//= Nearest Bright Star
// Put name of star, and difference in RA/Dec in User1...User3

sub main()
	dim limiting_mag      as double
	dim delta_ra          as double
	dim delta_dec         as double
	dim final_delta_ra    as double
	dim dec_factor        as double
	dim east_or_west      as string
	dim north_or_south    as string
	
	limiting_mag = 3.0
	dim dlg as new Dialog
	dlg.DoubleParameter("Limiting Magnitude", limiting_mag)
	if not dlg.Show then return
	limiting_mag = dlg.DoubleParameter("Limiting Magnitude")
	
	call Plan().NewUserFieldDefinition("Star", APUserField.ufType_String)
	call Plan().NewUserFieldDefinition("Star E/W", APUserField.ufType_String)
	call Plan().NewUserFieldDefinition("Star N/S", APUserField.ufType_String)
	
	for i as integer = 1 to Plan().nObjects
		dim ob as APPlanObject = Plan().Obj(i)
		dim star as APCatalogObject = ob.ClosestStar(limiting_mag)
		
		if star <> nil then
			delta_ra  = ob.RA  - star.RA
			delta_dec = ob.Dec - star.Dec
			
			if ob.RA > star.RA then
				east_or_west = "E"
			else
				east_or_west = "W"
			end if
			if ob.Dec > star.Dec then
				north_or_south = "N"
			else
				north_or_south = "S"
			end if
			if abs(delta_ra) > 12.0 then
				delta_ra = 24.0 - abs(delta_ra)
				if east_or_west = "E" then
					east_or_west = "W"
				else
					east_or_west = "E"
				end if
			end if
			
			dec_factor     = cos(ob.Dec * DegreesToRadians)
			final_delta_ra = delta_ra * 15.0 * dec_factor
			
			ob.UserField("Star").Value = star.Bayer
			ob.UserField("Star E/W").Value = east_or_west+" "+str(round(abs(final_delta_ra)))+DegreeSymbol
			ob.UserField("Star N/S").Value = north_or_south+" "+str(round(abs(delta_dec)))+DegreeSymbol
		end if
	next
end sub