APSF,V1,0,,-1,0,
// Save double star observations to tab-separated text file
//= Save DS Obs

sub main()
	// Restore saved parameters from plan document, 
	// defaulting the first time
	
	dim FirstDate as double = Plan().RestoreValue("FirstDate",Date.ParseDate("2010-01-01"))
	dim IncludeNotes as boolean = Plan().RestoreValue("IncludeNotes",false)
	
	// Edit parameter values, if necessary
	
	dim dlg as new Dialog
	
	dlg.DateParameter("First Date to Consider",FirstDate)
	dlg.BooleanParameter("Include Notes",IncludeNotes)
	
	if not dlg.Show then return
	
	FirstDate = dlg.DateParameter("First Date to Consider")
	IncludeNotes = dlg.BooleanParameter("Include Notes")
	
	// Create output tab-separated text file
	dim f as APTextFile = APTextFile.WriteFile("DSObs.txt")
	if f = nil then return
	
	dim line(-1) as string
	line.Append "Date/Time"
	line.Append "ID"
	line.Append "Name"
	line.Append "RA"
	line.Append "Dec"
	if IncludeNotes then line.Append "Notes"
	
	f.WriteTabSeparatedLine line
	
	// Loop through objects
	
	for i as integer = 1 to APObservation.Count
		dim obs as APObservation = APObservation.Observation(i)
		if obs.Type.Contains("Dbl") and obs.LocalDateTime>=FirstDate then
			redim line(-1)
			dim d as new Date(obs.LocalDateTime)
			line.Append d.SQLDateTime
			line.Append obs.ID
			line.Append obs.Name
			line.Append FormatRA(obs.RA)
			line.Append FormatDec(obs.Dec)
			if IncludeNotes then line.Append obs.Notes
			
			f.WriteTabSeparatedLine line
		end if
	next
	f.Close
	
	// Save non-volatile parameters to plan document
	
	Plan().SaveValue("FirstDate",FirstDate)
	Plan().SaveValue("IncludeNotes",IncludeNotes)
end sub